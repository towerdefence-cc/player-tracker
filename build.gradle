plugins {
    id("java")
    id("org.springframework.boot") version "2.7.4"
    id("io.spring.dependency-management") version "1.0.14.RELEASE"

    id("io.freefair.lombok") version "6.5.1"
}

group = "cc.towerdefence.api"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = "https://repo.towerdefence.cc/snapshots" }
}

dependencies {
    implementation("net.devh:grpc-server-spring-boot-starter:2.13.1.RELEASE") {
        exclude group: "io.grpc", module: "grpc-netty-shaded" // causes version conflicts, provided by the proto SDK
    }

    implementation("cc.towerdefence.api:grpc-sdk:22f2328")

    implementation("org.springframework.boot:spring-boot-starter-data-mongodb")

    developmentOnly("org.springframework.boot:spring-boot-devtools")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

bootBuildImage {
    imageName = "towerdefence-cc/player-tracker:dev"

    docker {
        publishRegistry {
            url = "https://ghcr.io"
            token = System.getenv("REGISTRY_TOKEN")
        }
    }
}

tasks.named("test") {
    useJUnitPlatform()
}
